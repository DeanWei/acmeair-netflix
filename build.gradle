defaultTasks 'clean', 'build'

buildscript {
    repositories { mavenCentral() }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	
    group = "com.acmeair.${rootProject.name}"
    version = rootProject.version
    sourceCompatibility = 1.6
    
    dependencies {
	compile 'com.netflix.hystrix:hystrix-core:1.4.0-RC4'
	compile 'com.netflix.ribbon:ribbon:2.0-RC3'
	compile 'com.netflix.ribbon:ribbon-core:2.0-RC3'
	compile 'com.netflix.ribbon:ribbon-httpclient:2.0-RC3'
	compile 'com.netflix.karyon:karyon-jersey-blocking:2.1.00-RC2'
	compile 'com.netflix.karyon:karyon-admin-web:2.1.00-RC2'
    }
}	

task wrapper(type: Wrapper) {
	gradleVersion = '1.9'
}

project(':acmeair-common') {
	dependencies {
	}
}

project(':acmeair-services') {
	dependencies {
		compile project(':acmeair-common')
	}
}

project(':acmeair-services-astyanax') {
	dependencies {
		compile project(':acmeair-common')
		compile project(':acmeair-services')
		compile 'com.datastax.cassandra:cassandra-driver-core:1.0.5'
	}
}

project(':acmeair-webapp') {

	dependencies {
		compile project(':acmeair-common')
		compile project(':acmeair-services')
		compile project(':acmeair-services-astyanax')
		
		compile	'com.netflix.ribbon:ribbon-eureka:2.0-RC3'
	}
}

project(':acmeair-ribbon-test') {
	apply plugin: 'application'
	dependencies {
		runtime 'com.netflix.ribbon:ribbon-eureka:2.0-RC3'
		runtime 'com.sun.jersey:jersey-json:1.17.1'
		runtime 'com.sun.jersey:jersey-bundle:1.17.1'
		runtime 'com.sun.jersey:jersey-server:1.17.1'
		runtime 'com.sun.jersey:jersey-core:1.17.1'
		runtime 'org.slf4j:slf4j-log4j12:1.7.2'
	}
	run {
		systemProperty 'java.awt.headless', 'true'
		jvmArgs = ["-Xdebug", "-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=8000"]
		args "com.acmeair.tests.ribbon.RibbonTestApp"
	}

	mainClassName = "com.netflix.karyon.KaryonServer"
}
	
project(':acmeair-auth-service') {
	apply plugin: 'application'
	dependencies {
		compile project(':acmeair-common')
		compile project(':acmeair-services')
		compile project(':acmeair-services-astyanax')
		
		runtime 'com.sun.jersey:jersey-json:1.17.1'
		runtime 'com.sun.jersey:jersey-bundle:1.17.1'
		runtime 'com.sun.jersey:jersey-server:1.17.1'
		runtime 'com.sun.jersey:jersey-core:1.17.1'
	}
	run {
		systemProperty 'java.awt.headless', 'true'
//		jvmArgs = ["-Xdebug", "-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=8000"]
		args "com.acmeair.services.authService.AuthServiceApp"
	}
	
	mainClassName = "com.netflix.karyon.KaryonServer"
}

project(':acmeair-loader') {
	apply plugin: 'application'
	mainClassName = 'com.acmeair.loader.Loader'
	
	dependencies {
		compile project(':acmeair-common')
		compile project(':acmeair-services')
		compile project(':acmeair-services-astyanax')
	}
}

project(':acmeair-driver') {
	dependencies {
		compile 'com.googlecode.json-simple:json-simple:1.1.1'
		compile 'org.apache.jmeter:ApacheJMeter_core:2.7'
	}
}
